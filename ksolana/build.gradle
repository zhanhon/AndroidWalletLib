plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false //混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs = freeCompilerArgs + "-Xopt-in=kotlin.RequiresOptIn"
//        jvmTarget = "1.8"
//        apiVersion = "1.6"
//        languageVersion = "1.6"
//        freeCompilerArgs = listOf(
//                "-Xopt-in=" +
//                        "kotlinx.serialization.InternalSerializationApi," +
//                        "kotlinx.serialization.ExperimentalSerializationApi"
//        )
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    // Json lib
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    // Crypto
    implementation 'com.github.komputing:KBase58:0.2'
    implementation 'io.github.instantwebp2p:tweetnacl-java:1.1.2'
}


